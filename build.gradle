group 'CoolCrowd'
version '0.5-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'nu.studer.jooq'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'application'
apply plugin: 'no.nils.wsdl2java'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'mysql:mysql-connector-java:5.+'
        classpath 'nu.studer:gradle-jooq-plugin:1.0.5'
        classpath "net.saliman:gradle-cobertura-plugin:2.3.+"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.+"
        classpath 'no.nils:wsdl2java:0.9'
    }
}

mainClassName = 'edu.kit.ipd.crowdcontrol.objectservice.Main'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'org.jooq:jooq:3.7.1'
    compile 'org.jooq:jooq-meta:3.7.1'
    compile 'org.jooq:jool:0.9.7'
    compile 'mysql:mysql-connector-java:5.+'
    compile 'org.mybatis:mybatis:3.+'
    compile 'com.zaxxer:HikariCP:2.+'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.mashape.unirest:unirest-java:1.4.7'
    compile 'com.google.protobuf:protobuf-java:3.+'
    compile 'commons-validator:commons-validator:1.5.0'
    compile 'com.google.protobuf:protobuf-java-util:3.+'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'javax.mail:mail:1.4.7'
    compile 'org.apache.commons:commons-io:1.3.+'
    compile 'org.apache.logging.log4j:log4j-api:2.5'
    compile 'org.apache.logging.log4j:log4j-core:2.5'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.0'
    compile 'org.jyaml:jyaml:1.3'
    compile ('com.sparkjava:spark-core:2.3') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'junit:junit:4.12'
}

//tests

test {
    testLogging {
        events 'failed', 'skipped', 'passed'
    }
}

sourceSets {
    unitTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/java')
        }

        resources.srcDir file('src/test/resources')
    }

    restTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/rest-test/java')
        }

        resources.srcDir file('src/rest-test/resources')
    }

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }

        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    restTestCompile.extendsFrom testCompile
    restTestRuntime.extendsFrom testRuntime
}

task restTest(type: Test) {
    jacoco {
        destinationFile = file("$buildDir/jacoco/restTest.exec")
        classDumpFile = file("$buildDir/classes/restTest")
    }

    testClassesDir = sourceSets.restTest.output.classesDir
    classpath = sourceSets.restTest.runtimeClasspath

    testLogging {
        events 'failed', 'skipped', 'passed'
    }
}

task integrationTest(type: Test) {
    jacoco {
        destinationFile = file("$buildDir/jacoco/integrationTest.exec")
        classDumpFile = file("$buildDir/classes/integrationTest")
    }

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    testLogging {
        events 'failed', 'skipped', 'passed'
    }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    executionData = fileTree(dir: 'build/jacoco', include: '**/*.exec')

    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['edu/kit/ipd/crowdcontrol/objectservice/proto/**',
                              'edu/kit/ipd/crowdcontrol/objectservice/database/model/**'])
        })
    }
}

//jooq database-model generation
jooq {
    sample(sourceSets.main) {
        jdbc {
            url = 'jdbc:mysql://localhost:8888'
            driver = 'com.mysql.jdbc.Driver'
            user = 'LeanderK'
            password = ''
            schema = 'crowdcontrol'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.mysql.MySQLDatabase'
                inputSchema = 'crowdcontrol'
            }
            generate {
                daos = true
            }
            target {
                packageName = "edu.kit.ipd.crowdcontrol.objectservice.database.model"
                directory = 'src/main/java'
            }
        }
    }
}
generateSampleJooqSchemaSource.enabled = false

file("src/main/generated/").mkdirs()

wsdl2java {
    generatedWsdlDir = file("src/main/generated/")  // target directory for generated source coude
    wsdlDir = file("src/main/generated/") // define to support incremental build
    wsdlsToGenerate = [['src/main/wsdl/mturk/AWSMechanicalTurkRequester.wsdl']]
    locale = Locale.ENGLISH
}